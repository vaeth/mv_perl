#!/usr/bin/env perl
require 5.012;
package TouchDifference v1.4.2;

use strict;
use integer;
use Getopt::Long 2.24 ();
#use Time::localtime (); # if verbose is used
#use Pod::Usage ();

=head1 NAME

touch.difference - correct times of files, e.g. by a daylight saving offset

=head1 SYNOPSIS

B<touch.difference> [options] I<File1> [I<File2> I<...>]

To get an extended help, type B<touch.difference --man>

=head1 DESCRIPTION

This script modifies the timestamps of all files given as arguments
by adding or subtracting a certain amount of time given in the options.

Even if you do not specify that something is added or subtracted,
the timestamps will be modified, in general, since the accuracy of this
script is only in seconds, i.e. if your filesystem has a more accurate
resolution than 1 second, the timestamps are modified to be a full second.
This is considered a feature, since it simplifies comparing timestamps
of different filesystems with different resolutions of timestamps.

=head1 OPTIONS AND ARGUMENTS

=over 8

=item B<--help> or B<-h>

Display brief help

=item B<--man> or B<-?>

Display extended help as a manpage

=item B<--version> or B<-V>

Print version number and exit

=item B<--add=>I<seconds> or B<-a> I<seconds>

Add I<seconds> to the file mtime/atime timestamps

=item B<--sub=>I<seconds> or B<-s> I<seconds>

Sub I<seconds> from the file mtime/atime timestamps

=item B<--noatime>

Do not modify the atime

=item B<--nomtime>

Do not modify the mtime

=item B<--verbose> or B<-v>

Be verbose

=item B<--dry-run> or B<-n>

Do not actually modify the times

=item B<-->

Last option

=back

=head1 AUTHOR

Martin VE<auml>th E<lt>martin@mvath.deE<gt>

=cut

# Default for the Options:

my $seconds = 0;
my $verbose = '';
my $dry = '';
my $addatime = 1;
my $addmtime = 1;

# Global variables:

my $name = 'touch.difference';
our $VERSION;

# Functions:

sub version {
	print($name, ' ', $VERSION, "\n");
	exit(0)
}

sub pod2usage {
	require Pod::Usage;;
	Pod::Usage::pod2usage(@_)
}

# Parse Options:

Getopt::Long::Configure(qw(bundling gnu_compat));
Getopt::Long::GetOptions(
	'help|h', sub { &pod2usage(0) },
	'man|?', sub { &pod2usage(-verbose => 2, -exit => 0) },
	'version|V', \&version,
	'verbose!', \$verbose,
	'v', \$verbose,
	'dry-run!', \$dry,
	'n', \$dry,
	'atime!', \$addatime,
	'mtime!', \$addmtime,
	'add|a=i', \$seconds,
	'sub|s=i', sub { $seconds = -$_[1] }
) or &pod2usage(2);

&pod2usage(2) unless(@ARGV);

require Time::localtime if($verbose);

# Actual touch function:

sub dotouch {
	my ($file) = @_;
	my ($atime, $mtime) = ((stat($file))[8, 9]);
	return unless(defined($atime) && defined($mtime));
	$atime += $seconds if($addatime);
	$mtime += $seconds if($addmtime);
	printf("%s %s (%s)\n", $file,
		Time::localtime::ctime($mtime),
		Time::localtime::ctime($atime)) if($verbose);
	utime($atime, $mtime, $file) unless($dry)
}

# Main part

for my $file (@ARGV) {
	&dotouch($file)
}
1;
