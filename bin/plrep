#!/usr/bin/env perl
BEGIN { require 5.006_001 }
package Plrep;
our $VERSION = 'v2.15.1';
eval { $VERSION = version->declare($VERSION) };
eval 'use version 0.77; $VERSION = version->declare($VERSION)' if ($@);

use warnings;
use strict;
use integer;
use Getopt::Long 2.24 ();
use File::Find ();
use Term::ANSIColor ();
#use Pod::Usage ();

# Colors for matches on the various terminal types:
# Format is an array of the form [grep, "old" content, "new" content]

my @termlist = (
  ['bold yellow', 'bold red', 'bold green'],
  ['bold magenta', 'bold red', 'bold blue'],
  ['reverse', 'underline reverse', 'bold underline reverse']
);

my $backspace_patternn = qr{[\b]|\x1b\[3\~};

=encoding UTF-8

=head1 NAME

plrep - search and replace regular expressions in multiple files interactively

=head1 SYNOPSIS

B<plrep> [options] I<Search> I<Replace> [I<File1> I<File2> I<...>]

A properly implemented multiline mode is in the successor project: replacer

To get an extended help, type B<plrep --man> or B<plrep -?>

=head1 PROBLEMS

If part of the line magically "vanishes" this may happen because a match is
printed in the background color. Use the B<--terminal> option to change this.

=head1 OPTIONS AND ARGUMENTS

=over 8

=item B<--help> or B<-h>

Display brief help.

=item B<--man> or B<-?>

Display extended help as a manpage.

=item B<--terminal=>I<number> or B<--highlight=>I<number>

Choose terminal color-set (i.e. way matches are highlighted) to type I<number>.
Every I<number> is admissible but intentionally not documented which colors
are used. The default is I<number>=0, and a "secure" version which should
display visible and readable (though not very beautiful) on every terminal
is I<number>=-1. This option cannot be used to switch off highlighting:
Use B<-H> for the latter.

=item B<--multiline> or B<-m> (switch)

Multiline mode (experimental: not all other options are implemented for this
mode, and partially they are interpreted slightly differently).

A complete implemention of multiline mode is in a successor project: replacer

=item B<--yes> or B<-y> (switch)

Assume 'yes' on all queries except filechanges.

=item B<--always> or B<-Y> (switch)

Assume 'yes' on all queries and be quiet.

=item B<--verbose> or B<-v> (switch)

Be verbose (only useful after -Y).

=item B<--ignore-case> or B<-i> (switch)

Ignore case in 'Search'.

=item B<--binary> or B<-b> (switch)

Open in binary mode.

=item B<--plain-search> or B<--search-plain> or B<-p> (switch)

Use plain string for I<Search>
(i.e. do not interpret it as a regular expression).

=item B<--plain-replace> or B<--replace-plain> or B<-P> (switch)

Use plain string for I<Replace>
(i.e. do not interpret it as a regular expression).

=item B<--never-highlight> or B<-H> (switch)

Do not highlight matches (e.g. for redirection to file).

=item B<--recurse>[B<=>I<dir>] or B<-r> I<dir> (switch)

Recurse down from Directory I<dir>. If I<dir> is omitted, '.' is assumed.
If no file arguments I<File1> I<File2> I<...> are given, all readable files
from I<dir> downwards are searched (symbolic links of directories are ignored).
Otherwise, I<File1> I<...> are interpreted as masks for the required files
including their paths (note that this means that B<filename> as I<File1>
will usually not mean what you might expect: Probably you wanted to use
instead e.g. B<./filename> or B<*/filename>).
Do not forget to quote I<File1> I<...> in this case (because the shell might
substitute wildcards). To avoid the frequent problem with forgotten quotes,
each of I<File1> I<...> must contain wildcards, otherwise an error is reported.
To suppress this test, use B<recurse-suppress>.

=item B<--recurse-suppress>[B<=>I<dir>] or B<-R> I<dir> (switch)

As B<--recurse> but do not report an error if some of I<File1> I<...> does not
contain a wildcard.

=item B<--file>[B<=>I<file>] or B<-f> I<file> (switch)

Assume that there are no I<Search> and I<Replace> arguments.
Read instead these arguments from I<file> (or from stdin if B<=>I<file>
is omitted or cannot be opened). This avoids all problems with undesired
pattern replacement by the shell.

=item B<--context=>I<num> or B<-C> I<num>

Print I<num> lines of context before and after match.

=item B<--begin=>I<num> or B<-B> I<num>

Print I<num> lines of context before match.

=item B<--after=>I<num> or B<-A> I<num>

Print I<num> lines of context after match.

=item B<--grep> or B<-g> (switch)

Grep mode: Do not read the I<Replace> argument and only print matches.

=item B<--count> or B<-c> (switch)

Count only matches (implies B<--grep>).

=item B<--list> or B<-l> (switch)

List only files which contain matches (implies B<--grep>)

=item B<--keeptimestamp> or B<-k> (switch)

Do not change the timestamps of modified files

=item B<--nonprintable=>I<character> or B<-n> I<character>

Display all nonprintable characters as I<character>. The default value of
I<character> is B<~>. If I<character> is empty, then nonprintable characters
are not treated differently.

=item B<--truncate=+>/B<->I<num> or B<-t +>/B<->I<num> (switch)

Truncate the output to at most I<num> symbols (for the line with the match).
With negative sign, the line is simply cut after I<num> symbols so that it
might happen that the place of the match is not displayed.
However, with positive sign (the B<+> may be omitted, of course),
the output line is possibly shifted such that
at least 3 symbols from the end of the match are visible. You can modify
this default with the following option:

=item B<--Truncate=>[B<s>|B<e>]I<num> or B<--shift=>[B<s>|B<e>]I<num> or
B<-T> [B<s>|B<e>]I<num>

Show at least I<num> symbols from the end (with B<e> or by default)
respectively from the start (with B<s>) of the match.
I<num> may also be 0 or negative.

=item B<--version> or B<-V>

print version number

=item B<-->

Last option

=back

The options marked with (switch) act also as switches, i.e. their negation
(which is always the default) can be obtained be proceeding 'no' to the
(long form of) the option.
E.g. B<--nobinary> negates the effect of a previous option B<--binary> or
a previous B<-i>.

=head1 THE SEARCH STRING

The syntax of the I<Search> string is essentially the syntax of
Perl regular expressions (see B<man perlre> for an extended help).

Since for security reasons the string is pre-interpreted and since
the particular features depend on the Perl version, not everything
described in the Perl manpage for regular expressions is guaranteed to work.
Moreover, some flags are set implicitly (e.g. with option B<--ignore-case>).

The most important "magic" characters are explained here very briefly
by examples.

=over 8

=item B<.>

Any character

=item B<A*>

None or more repetitions of B<A>

=item B<A+>

One or more repetitions of B<A>

=item B<A?> or B<A{0,1}>

Optional B<A> (=none or one repetition of B<A>)

=item B<A{4}B{2,4}>

B<AAAA> followed by 2,3 or 4 repetitions of B<B>

=back

To make B<*> B<+> B<?> and B<{,}> non-greedy, place a B<?> behind


=over 8

=item B<A|B>

B<A> or B<B>


=item B<(AB)+>

Group B<AB>, one or more repetitions of this group

B<\1>

Same content as first group braced with B<(>I<...>B<)>

=item B<^A>

B<A> at beginning of line

=item B<A$>

B<A> at end of line

=back

=over 8

=item B<\b> respectively <\B>

word boundary (respectively no word boundary)

=item B<[A-Z\b]>

any capital letter or backspace (B<\b> is admissible only in B<[>I<...>B<]> -
otherwise, it means word boundary)

=item B<[^A-Z\d]>

all except capital letter or digit

=item B<\d> respectively B<\D>

digit [0-9] (respectively all except digit)

=item B<\w> respectively <\W>

alphanumeric [a-zA-Z0-9_] (respectively all except alphanumeric)

=item B<\s> respectively B<\S>

whitespace (respectively all except whitespace)

=item B<\r> B<\n> B<\f> B<\t> B<\e> B<\a>

special characters: return newline formfeed tab escape alarm

=item B<\115> B<\x4d> B<\x4D>

character with octal value 115 (hexadezimal value 4d) (=B<M>)

=item B<\>I<character>

usually matches I<character>, e.g. B<\(> matches B<(>

=back

=over 8

=item B<(?=>I<...>B<)>

Look ahead: Matches if I<...> would match next.

=item B<(?!>I<...>B<)>

Lookahead: Matches if I<...> would not match next.

=item B<(?<=>I<...>B<)>

Lookbehind: Match if I<...> preceeds. I<...> must have fixed length.

=item B<(?<=>I<...>B<)>

Lookbehind: Match if I<...> does not preceed. I<...> must have fixed length.

=item B<(?>>I<...>B<)>

Treat I<...> as an independent pattern, as if it should match alone.
In particular, no backtracking is used.
For example, "^(?>a*)a" never matches, because the "a"s are eaten.
The effect is essentially the same as B<(?=(>I<...>B<))\1>.

=item B<(?(>I<condition>B<)>I<yes_pattern>B<)> or B<(?(>I<condition>B<)>I<yes_pattern>B<|>I<no_pattern>B<)>

Conditional evaluation. I<condition> is either a number (in paranthesis),
or a lookahead/lookbehind assertion. If it is a number, the condition
is true if the corresponding group had matched.

=item B<(?I<imsxB<->imsx>:>I<...>B<)>

Switch modifier B</i> or B</m> or B</s> or B</x> on (or off if behind B<->).
Modifiers are:

=over 4

=item B<i>

ignore case

=item B<m>

multiple lines (let ^ and $ match at newlines)

=item B<s>

treat string as single line (. matches also newline),

=item B<x>

Comment-style pattern: Ignore whitespace, B<#> comments to end of line.

=back

=item B<(?:>I<...>B<)>

A group which does not count as a group (i.e. gives no reference).

=item B<(?I<imsxB<->imsx>:>I<...>B<)>

As B<(?:>I<...>B<)>, but use flag B<imsx-imsx> for the group

=item B<(?#>I<...>B<)>

A comment: Ignores the content of the group

=back

Examples for regular expressions in the I<Search> argument:

=over 8

=item B<E<lt>f.?l+\.E<gt>>

matches B<E<lt>full.E<gt>>, B<E<lt>fill.E<gt>>, or B<E<lt>fl.E<gt>> but not B<E<lt>feel.E<gt>>, B<E<lt>fileE<gt>>, nor B<E<lt>ff.E<gt>>.

=item B<(.*) \(or not \1\)>

matches B<to be (or not to be)> but not B<a bee (or not a B)>

=item B<A(.*)a>

matches from B<AlphaBetaB> the substring B<AlphaBeta>

=item B<A(.*?)a>

matches from B<AlphaBetaB> the substring B<Alpha>

=item B<A(?=.*ph.*ta)(.*)a>

matches from B<AlphaBetaB> the substring B<AlphaBeta>

=back

=head1 THE REPLACE STRING

The I<Replace> string has more or less the features of a quoted string
(B<">I<...>B<"> or B<qq{>I<...>B<}>) within perl (see B<man perlop>, section
B<Quote and Quote-like operators>, for more details).

The main difference is that the magic strings B<$1> B<$2> ...
(and, with the same meaning, B<%1> B<%2> ...)
are available which expand to the match for the corresponding group of
the I<Search> string expression. Another difference is that other perl
functions/variables are not accessible for security reasons (e.g. unless
followed by a number, B<$> always means the symbol B<$> and not a variable).

Since for security reasons the string is pre-interpreted and since
the particular features depend on the Perl version, not everything
described in the Perl manpage for quoted strings is guaranteed to work.

The most important "magic" characters are explained here very briefly
by examples.

=over 8

=item B<$1> B<$2> B<$10>

Match of group B<1>, B<2>, B<10> of the I<Search> string.

=item B<${1}0>

Match of group B<1> in I<Search> string, followed by the symbol B<0>.

=item B<%1> B<%2> B<%{1}0> B<%10>

This is equivalent to B<$1> B<$2> B<${1}0> and B<$10>, respectively.

=item B<\a> B<\b> B<\e> B<\f> B<\n> B<\r> B<\t>

Special characters: alarm backspace escape formfeed newline return tab

=item B<\115> B<\x4d> B<\x4D>

Character with octal value 115 (hexadezimal value 4d)

=item B<\l> B<\u>

Next letter lowercase/uppercase

=item B<\L>I<...>B<\E> B<\U>I<...>B<\E>

Put I<...> in lowercase/uppercase

=item B<\>I<character>

Character I<character>

=back

=head1 EXAMPLES OF USAGE

=over 8

=item B<plrep -r . '\{(.*)theorem\}' '{t:\1}' '*.tex'>

Replaces interactively in all *.tex files of the current directory and
its subdirectories phrases like B<{>I<...>B<theorem}> by B<{t:>I<...>B<}>

=item B<plrep -pPY '\1' '\2' *>

Replace silently in all files of the current directory the phrase B<\1> by B<\2>

=item B<plrep -pPy '\1' '\2' *>

Same as above, but display changes and ask before writing to a file.

=item B<plrep -g '\{t:[^\}]*\}' *>

Display (highlighted) all occurences of the phrase B<{t:>I<...>B<}> in all
files of the current directory.

=back

=head1 AUTHOR

Martin VE<auml>th E<lt>martin@mvath.deE<gt>

=cut

# Options and their defaults:

my $always_replace = '';
my $always_write = '';
my $grep_only = '';
my $count_only = '';
my $list_only = '';
my $keep_timestamp = '';
my $ignore = '';
my $binary = '';
my $multiline = '';
my $plain_search = '';
my $plain_replace = '';
my $recurse = '';
my $from_file = '';
my $not_highlight = '';
my $before = 0;
my $after = 0;
my $trunc_opt = '';
my $shift_opt = '';
my $verbose = '';
my $nonprintable = '~';
my $data_name;
my $recurse_dir;
my $suppress;
my $terminal = $termlist[0];

# Global variables:

my $name = 'plrep';
my $show_changes = 1;
my $truncate_mode = 0;
my $truncate;
my $shift;
my $shift_from_end;
my $file_name;
my $last_printed;
my $file_matches;
my $line;
my $current;

my @content = ();
my @substrings = ();
my @replacements = ();
my $ask_for_replace = '';
my $total_matches = 0;

my $search_pattern;
my $search;
my $replace;

sub version {
  print($name, ' ', $VERSION, "\n");
  exit(0)
}

sub pod2usage {
  require Pod::Usage;
  &Pod::Usage::pod2usage
}

sub fatal {
  print(STDERR join(@_, "\n"), "\n");
}

sub normal_exit {
  exit(0) unless ($grep_only);
  exit(127) if ($total_matches > 127);
  exit($total_matches)
}

sub opt_colored {
  my $string = shift();
  my $opt = shift();
  $opt ? Term::ANSIColor::colored($string, $_[0]) : $string
}

#Output line string_list formatted, colorized, and truncated.
#Two or three arguments: mode, line_number(-1), line
#
#mode > 0: line with "old" content or in grep mode a line with a match.
#mode < 0: line with "new" content.
#mode = 0: Informal content (no match) - used e.g. for pure "context" lines.
#
#If mode = 0, the third argument line must be given.
#Otherwise, the line is assumed to be contained in @substrings, where
#@substrings is "until 1st match" "1st match" "1st match to 2nd match" ...
#@substrings must contain at least 2 elements, because the first match is
#used to shift the line.
#
#It is assumed that the last character is a newline.

{ # static variables for print_line
  my $head_ellipsis = '...';
  my $tail_ellipsis = "...\n";
  my $len_tail_ellipsis = length($tail_ellipsis) - 1;  # Do not count \n
  my $len_head_ellipsis = length($head_ellipsis);
sub print_line {
  my ($mode, $line_number, $string) = @_;
  ++$line_number if ($line_number ne '');
  if ($mode == 0) {
    $string =~ s{[^\n[:print:]]}{$nonprintable}go if ($nonprintable ne '')
  }
  my ($mode_string, $color);
  if ($grep_only) {
    $mode_string = '';
    $color = $terminal->[0]
  } elsif ($mode > 0) {
    $mode_string = '-';
    $color = $terminal->[1]
  } elsif ($mode < 0) {
    $mode_string = '+';
    $color = $terminal->[2]
  } else {
    $mode_string = ' '
  }
  my $pre_string = $file_name . ':' . $line_number . $mode_string . ':';
  my $pre_string_len = length($pre_string);

  # Calculate beg, end (set cut_end to true if end should be used), and
  # len (if truncate_mode is used). len is the len without trailing \n
  # It may happen that end < beg in which case we print only ellipsis...

  my ($beg, $end, $cut_end, $len);
  if ($truncate_mode) {
    $end = $truncate - $pre_string_len;
    $cut_end = 1;
    if ($mode) {
        $len = 0;
        for (@substrings) {
          $len += length
        }
    } else {
        $len = length($string)
    }
    if (--$len < 0) {
        $len = 0
    }
    if ($len <= $end) {
        $beg = 0;
        $cut_end = ''
    } elsif ($mode && ($truncate_mode > 0)) {  # We shift only if match exists
        my $point = $shift + length($substrings[0]);
        if ($shift_from_end) {
          $point += length($substrings[1])
        }
        if ($point <= $end - $len_tail_ellipsis) {
          # The line is visible to the desired point
          $beg = 0; $end = $end - $len_tail_ellipsis
        } else {
          if ($len <= $point) {  # shift reaches lineend
              $beg = $len - $end + $len_head_ellipsis;
              $cut_end = ''
          } else {
              $beg = $point - $end + $len_head_ellipsis + $len_tail_ellipsis;
              $end = $point
          }
        }
    } else {  # truncate, but do not shift
        $beg = 0;
        $end -= $len_tail_ellipsis
    }
  } else {
    $beg = 0;
    $cut_end = ''
  }

  # Print according to beg and end

  # First, print the pre_string and head_ellipsis (if beg > 0).
  # If this string or our final string becomes too long already with
  # the above calculated minimal size, we print ellipsis and are finished.
  if ($truncate_mode) {
    my ($l, $i);
    if ($cut_end) {
        $l = $len_tail_ellipsis
    } elsif ($beg) {
        $i = $len - $beg + $len_head_ellipsis;
        $l = (($i < $len_tail_ellipsis) ? $i : $len_tail_ellipsis);
    } else {
        $l = (($len < $len_tail_ellipsis) ? $len : $len_tail_ellipsis)
    }
    if (($pre_string_len + $l) > $truncate) {
        if ($truncate > $len_tail_ellipsis) {
          print(substr($pre_string, 0, $truncate - $len_tail_ellipsis))
        }
        print($tail_ellipsis);
        return
    }
    print($pre_string);
    if ($cut_end) {
        if ($beg >= $end) {  # Our (minimal) calculation does not fit into the line
          print($tail_ellipsis);
          return
        }
    } elsif ($beg) {  # We have calculated to print head_ellipsis + string_until_end
        if ($pre_string_len + $i > $truncate) {  # too long
          print($tail_ellipsis);
          return
        }
    }
    if ($beg) {
        print($head_ellipsis)
    }
  } else {
    print($pre_string)
  }

  # Now the main print loop

  if ($mode) {
    my $is_a_match = '';
    $len = 0;
    for my $s (@substrings) {
      my $i = $s;
      $i =~ s{[^\n[:print:]]}{$nonprintable}go if ($nonprintable ne '');
      if ($i ne '') {
        if ($beg || $cut_end) {  # We calculate only if we must care
          my $l = length($i);
          my $from = 0;
          my $to = $l;
          if ($cut_end && ($end < $len + $l)) {
            $to = $end - $len;
            $to = $l if ($l < $to)
          }
          if ($beg) {
            $from = $beg - $len;
            $beg = 0 if ($from < $l)
          }
          $len = $len + $l;
          if ($from < $to) {
            if ($from || ($to < $l)) {
              print(&opt_colored(substr($i, $from, $to - $from),
              $is_a_match && $mode, $color))
            } else {
              print(&opt_colored($i, $is_a_match && $mode, $color))
            }
          }
          if ($cut_end && ($end <= $len)) {
            print($tail_ellipsis);
            return
          }
        } elsif ($is_a_match) {
          print(&opt_colored($i, $mode, $color))
        } else {
          print($i)
        }
      }
      unless ($not_highlight) {
        $color = $terminal->[0] if ($is_a_match);
        $is_a_match = !$is_a_match
      }
    }
  } elsif ($cut_end) {
    print(substr($string, 0, $end)) if ($end > 0);
    print($tail_ellipsis)
  } else {
    print($string)
  }
}}

# The main functions to change all occurences in one line.

sub replace_ask {
  while () {
    print('y(es)/n(o)/r(est of file)/q(uit this file)/a(lways)/s(top all) ');
    my $key = <STDIN>;
    if ($key =~ $backspace_patternn) {
        next
    } elsif ($key =~ m{^[yj]}i){
        return 1
    } elsif ($key =~ m{^n}i) {
        return 0
    } elsif ($key =~ m{^q}i) {
        $ask_for_replace = -1;
        return 0
    } elsif ($key =~ m{^r}i) {
        $ask_for_replace = 0;
        return 1
    } elsif ($key =~ m{^s}i) {
        &normal_exit()
    } elsif ($key =~ m{^a}i) {
        $always_replace = 1;
        $ask_for_replace = 0;
        return 1
    }
  }
}

sub change_string {
  my ($line_number) = @_;
  my ($changed, $i, $j, $count, $subs, $do_replace, $have_printed_after, $r);
  $changed = '';
  $do_replace = 1;
  $subs = $replace if ($plain_replace);
  for ($count = 0; $#substrings >= 2;
    shift(@substrings), shift(@substrings), ++$count) {
    $subs = $replacements[$count] unless ($plain_replace);
    if ($subs eq $substrings[1]) {
        $substrings[2] = $substrings[0] . $substrings[1] . $substrings[2]
    } else {
        if ($show_changes) {
          &print_line(1, $line_number);
          $have_printed_after = '';
          if ($after > 0) {
              my $ellipsis;
              if ($line_number < $last_printed) {
                 $j = $last_printed + 1;
                 $ellipsis = 1
              } else {
                 $ellipsis = '';
                 $j = $line_number + 1
              }
              for (; ($j <= $line_number + $after) && ($j < $#content); ++$j) {
                 if ($ellipsis) {
                    print('...');
                    $ellipsis = ''
                 }
                 $have_printed_after = 1;
                 &print_line(0, $j, $content[$j]);
                 $last_printed = $j;
              }
          }
          print('---') if ($have_printed_after);
          $r = $substrings[1];
          $substrings[1] = $subs; &print_line(-1, $line_number);
          $substrings[1] = $r
        }
        if ($ask_for_replace) {
          $do_replace = &replace_ask()
        }
        if ($do_replace) {
          $changed = 1;
          $substrings[2] = $substrings[0] . $subs . $substrings[2];
        } else {
          $substrings[2] = $substrings[0] . $substrings[1] . $substrings[2]
        }
    }
    if ($ask_for_replace < 0) {
        $substrings[0] = $substrings[2];
        last
    }
  }
  $content[$line_number] = $substrings[0] if ($changed);
  $changed
}


# The function to process a single file (in singleline mode)

sub process_singleline {
  my ($fh) = @_;
  $last_printed = -1;
  my $changed = '';
  my $i = 0;
  my $will_print = 0;
  $file_matches = 0;
  unless ($grep_only) {
    @content = <$fh> or return ''
  }
  for ($i = 0; ; ++$i) {
    if ($grep_only) {
      unless (defined($line = <$fh>)) {
        last
      }
    } elsif ($i > $#content) {
      last
    } else {
      $line = $content[$i]
    }
    if ($line =~ m{$search_pattern}og) {
      if ($list_only) {
        print("$file_name\n");
        $total_matches = 1;
        last
      }
      if ((!$count_only) && ($show_changes || $grep_only)) {
        if ($before > 0) {
          my $j = $i - $before;
          $j = $last_printed + 1 if ($j <= $last_printed);
          if ($grep_only) {
            for (; $j < $i; ++$j) {
              &print_line(0, $j, $content[$j % $before])
            }
          } else {
            for (; $j < $i; ++$j) {
              &print_line(0, $j, $content[$j])
            }
          }
        }
        $last_printed = $i if ($i > $last_printed)
      }
      @substrings = ();
      @replacements = ();
      my $last_pos = 0;
      do {
        ++$file_matches;
        unless ($count_only) {
          unless ($grep_only || $plain_replace) {
            eval "push(\@replacements, \"$replace\")"
          }
          $_ = pos($line) - length($&) - $last_pos;
          if ($_ <= 0) {
            push(@substrings, '')
          } else {
            push(@substrings, substr($line, $last_pos, $_))
          }
          $last_pos = pos($line);
          push(@substrings, $&)
        }
      } while ($line =~ m{$search_pattern}og);
      unless ($count_only) {
        push(@substrings, substr($line, $last_pos))
      }
      if ($grep_only) {
        unless ($count_only) {
          &print_line(1, $i);
          $will_print = $after
        }
      } else {
        $changed = 1 if (&change_string($i));
        last if ($ask_for_replace < 0)
      }
    } elsif ($will_print > 0) {
      $last_printed = $i if ($i > $last_printed);
      --$will_print;
      &print_line(0, $i, $line)
    } elsif (($before > 0) && $grep_only) {
      $content[$i % $before] = $line
    }
  }
  if ($count_only && $file_matches) {
    print($file_name, ': ', $file_matches, "\n")
  }
  $total_matches += $file_matches;
  $changed
}

# The function to process a single file (in multiline mode)

sub process_multiline {
  my ($fh) = @_;
  return '' unless (read($fh, $current, (-s $fh)));
  my $changed = '';
  my $i = 0;
  $last_printed = -1;
  $file_matches = 0;

  # Scan all occurences and build corresponding @content/@replacements

  @content = ();
  @replacements = ();
  my $last_pos = 0;
  while ($current =~ m{$search_pattern}og) {
    if ($list_only) {
      print($file_name, "\n");
      $total_matches = 1;
      return ''
    }
    ++$file_matches;
    unless ($count_only) {
      unless ($grep_only || $plain_replace) {
        eval "push(\@replacements, \"$replace\")"
      }
      $_ = pos($current) - length($&) - $last_pos;
      if ($_ <= 0) {
        push(@content, '')
      } else {
        push(@content, substr($current, $last_pos, $_))
      }
      $last_pos = pos($current);
      push(@content, $&)
    }
  }
  print($file_name, ': ', $file_matches, "\n")
    if ($count_only && $file_matches);
  $total_matches += $file_matches;
  return '' if ($list_only || $count_only || !@content);
  push(@content, substr($current, $last_pos));

  # Now the multiline grep.
  # Currently, truncation is ignored in this mode, although one might discuss
  # whether this is the best choice.
  # Line numbers are not displayed yet in this mode.

  if ($grep_only) {
    @substrings = ();
    while (my ($i, $curr) = each(@content)) {
      if (($i & 1) != 0) {
        push(@substrings, $curr);
        next
      }
      my ($c, $d);
      if ($i < $#content) {
        $c = &find_newline($curr, -($before + 1));
        if ($c < 0) {
          push(@substrings, $curr);
          next
        }
        if ($i == 0) {
          push(@substrings, substr($curr, $c));
          next
        }
      }
      $d = &find_newline($curr, $after + 1);
      if ($d++ < 0) {
        push(@substrings, $curr);
        next
      }
      if ($i == $#content) {
        push(@substrings, substr($curr, 0, $d));
        next
      }
      if ($d >= $c) {
        push(@substrings, $curr);
        next
      }
      push(@substrings, substr($curr, 0, $d) . substr($curr, $c))
    }
    &print_line(1, '');
    return ''
  }

  # Now the interactive replacement in multiline mode.
  # Currently, this is only rudimentary implemented:
  # options for context lines and truncation are simply ignored.
  # Line numbers are not displayed yet in this mode.

  my $k = 0;
  my $do_replace = 1;
  my $subs;
  for (my ($i, $curr) = each(@content)) {
    if ($plain_replace) {
      $subs = $replace
    } else {
      $subs = $replacements[$k]
    }
    next if ($curr eq $subs);
    my ($p, $j);
    for ($j = $i - 1; $j >= 0; --$j) {
      $p = &find_newline($content[$j], -1);
      last if ($p >= 0)
    }
    if ($j < 0) {
      $j = $p = 0
    }
    $a = substr($content[$j], $p);
    if (++$j < $i) {
      $a = join('', $a, @content[$j .. ($i - 1)])
    }
    @substrings = ($a);
    push(@substrings, $curr);
    for ($j = $i + 1; $j < @content; ++$j) {
      $p = &find_newline($content[$j], 1);
      if ($p >= 0) {
        push(@substrings, substr($content[$j], 0, $p));
        last
      }
      push(@substrings, $content[$j]);
    }
    if (@substrings >= 3) {
      $substrings[$#substrings] .= "\n"
    } else {
      push(@substrings, "\n")
    }
    &print_line(1, '');
    $substrings[1] = $subs;
    &print_line(-1, '');
    $do_replace = &replace_ask() if ($ask_for_replace);
    if ($do_replace) {
      $changed = 1;
      $content[$i] = $subs
    }
    if ($ask_for_replace < 0) {
      keys(@content);  # reset "each" counter
      last
    }
  }
  $changed
}

# find_newline(string, n)
# if n>0 returns the index of the n-th newline.
# if n<0 returns the index following the (-n)-th newline from the end.
# if only k<|n| newlines are found, the negative value k-|n| is returned.

sub find_newline {
  my ($a, $count) = @_;
  my $l = length($a);
  for (my $i = 0; $i < $l; ++$i) {
    if ($count < 0) {
      if (substr($a, -$i, 1) eq "\n") {
        if (++$count == 0) {
          return ($l - $i)
        }
      }
    } else {
      if (substr($a, $i, 1) eq "\n") {
        return $i if (--$count == 0);
      }
    }
  }
  ($count > 0) ? (-$count) : $count
}

sub process_file {
  $ask_for_replace = ($always_replace ? 0 : 1);
  $multiline ? &process_multiline : &process_singleline
}

# Parse Options:

Getopt::Long::Configure(qw(gnu_getopt));
Getopt::Long::GetOptions(
  'help|h', sub { &pod2usage(0) },
  'man|?', sub { &pod2usage(-verbose => 2, -exit => 0) },
  'version|V', \&version,
  'ignore-case|i', \$ignore,
  'yes!', \$always_replace,
  'y', \$always_replace,
  'always!', \$always_write,
  'Y', \$always_write,
  'verbose!', \$verbose,
  'v', \$verbose,
  'context|C=i', sub { $before = $after = $_[1] },
  'before|B=i', \$before,
  'after|A=i', \$after,
  'grep!', \$grep_only,
  'g', \$grep_only,
  'count!', \$count_only,
  'c', \$count_only,
  'list!', \$list_only,
  'l', \$list_only,
  'keeptimestamp!', \$keep_timestamp,
  'k', \$keep_timestamp,
  'terminal|highlight=i', sub { $terminal = $termlist[$_[1] % ($#termlist)] },
  'never-highlight!', \$not_highlight,
  'H', \$not_highlight,
  'binary!', \$binary,
  'b', \$binary,
  'multiline!', \$multiline,
  'm', \$multiline,
  'nonprintable|n=s', \$nonprintable,
  'search-plain|plain-search!', \$plain_search,
  'p', \$plain_search,
  'replace-plain|plain-replace!', \$plain_replace,
  'P', \$plain_replace,
  'file|f:s', sub { $data_name = $_[1]; $from_file = 1 },
  'nofile', sub { $from_file = '' },
  'recurse|r:s', sub { $recurse_dir = $_[1]; $recurse = 1; $suppress = '' },
  'norecurse', sub { $recurse = '' },
  'recurse|recurse-suppress|R:s',
    sub { $recurse_dir = $_[1]; $recurse = $suppress = 1 },
  'norecurse|norecurse-suppress', sub { $recurse = $suppress = '' },
  'truncate|t=i', \$trunc_opt,
  'notruncate|no-truncate', sub { $trunc_opt = '' },
  'Truncate|shift|T=s', \$shift_opt
) or &pod2usage(2);

# Set default variables according to the options:

if ($always_write) {
  $always_replace = 1;
  $show_changes = $verbose
}
$grep_only = 1 if ($count_only || $list_only);

if ($trunc_opt ne '') {
  if ($trunc_opt < 0) {
    $truncate_mode = -1;
    $truncate = -$trunc_opt
  } else {
    $truncate_mode = 1;
    $truncate = $trunc_opt
  }
  $shift_from_end = '';
  if ($shift_opt ne '') {
    $shift_from_end = 1 if ($shift_opt =~ s{e}{}i);
    $shift_opt =~ m{^s?([+-]\d+)}i;
    $shift = (defined($1) ? $1 : 0)
  } else {
    $shift = 3
  }
}

# Read $search and $replace according to the options:

if ($from_file) {
  if (($data_name ne '') && open(my $para, '<', $data_name)) {
    $search = <$para>;
    chomp($search);
    unless ($grep_only) {
        $replace = <$para>;
        chomp($replace)
    }
    close($para)
  } else {
    print('Search : ');
    $search = <STDIN>;
    chomp($search);
    unless ($grep_only) {
        print('Replace: ');
        $replace = <STDIN>;
        chomp($replace)
    }
  }
} else {
  &pod2usage('no Search argument found') unless (@ARGV);
  $search = shift(@ARGV);
  unless ($grep_only) {
    &pod2usage('no Replace argument found') unless (@ARGV);
    $replace = shift(@ARGV)
  }
}

# Prepare the list of files to visit:

my @files = ();
if ($recurse) {
  if (@ARGV) {
    my $changed = 1;
    for my $i (@ARGV) {
      $_ = $i;
      s{([./^\$()])}{\\$1}g;
      if (!s{([?*])}{.$1}g) {  # If we have no ? or * joker, test for [...]:
        $i = "^$_\$";
        if ($changed && !$suppress) {  # No * or ? joker, but perhaps [...]?
          s{\\\\}{}g;  # First, eliminate quoted \ characters
          s{\\\[}{}g;  # Then,  eliminate quoted [ and ] charaters
          s{\\\]}{}g;
          $changed = '' unless(m{\[(.+)\]})
        }
      } else {
        $i = "^$_\$"
      }
    }
    &fatal('probably forgotten quote (recurse without joker)')
      unless ($changed)
  }
  $recurse_dir = '.' if ($recurse_dir eq '');
  no warnings 'File::Find';
  File::Find::find({wanted => sub {
    return unless (-r $_);
    if (!@ARGV) {
        push(@files, $File::Find::name);
        return
    } else {
        for my $i (@ARGV) {
          if ($File::Find::name =~ m{$i}) {
            push(@files, $File::Find::name);
            last
          }
        }
    }
  }}, $recurse_dir);
} else {
  if ((!@ARGV) && $grep_only) {
    push(@files, '')
  } else {
    for my $i (@ARGV) {
      push(@files, glob($i))
    }
  }
}

# Parse the $search and $replace strings:

$plain_search and ( $search =~ s{(\W)}{\\$1}g );
if ($search eq '') {  # Empty $search is special in perl
  $search = '.?'
}
if ($ignore) {
  $search_pattern = ($multiline ? qr{$search}oims : qr{$search}oi)
} else {
  $search_pattern = ($multiline ? qr{$search}oms : qr{$search}o)
}

unless ($grep_only || $plain_replace) {
  if ($replace =~ m{\W}) {
    $_ = $replace;
    $replace = '';
    while (m{\A(\w*)(\W)(.*)\z}m) {
      $replace .= $1;
      $_ = $3;
      if ($2 eq "\\") {
        if (m{\A(\W)(.*)\z}m) {
          $replace .= "\\$1";
          $_ = $2
        } elsif (m{^[xXabefnrtluLUE]}) {
          $replace .= "\\"
        } else {
          $replace .= "\\\\"
        }
      } elsif (($2 eq '$') || ($2 eq '%')) {
        if (m{^\d}) {
          $replace .= '$'
        } elsif (m{\A(\{\d+\})(.*)}m) {
          $replace .= "\$$1";
          $_ = $2
        }
      } else {
        $replace .= "\\" . $2
      }
    }
    $replace .= $_
  }
}

# Now process every file:

for my $fn (@files) {
  $file_name = $fn;
  if ($grep_only && (($file_name eq '') || ($file_name eq '-'))) {
    my $fh = \*STDIN;
    binmode($fh) if ($binary);
    &process_file($fh);
    next
  }
  open(my $fh, '<', $file_name) or next;
    # fatal("could not open $file_name for reading");
  binmode($fh) if ($binary);
  unless (&process_file($fh)) {
    close($fh);
    next
  }
  close($fh);
  next if ($grep_only);
  unless ($always_write) {
    my $key = '';
    while ($key eq '') {
      print("Write changes to $file_name? "
        . "y(es)/n(o)/w(rite always)/s(top all) ");
      $key = <STDIN>;
      if ($key =~ $backspace_patternn) {
        next
      } elsif ($key =~ m{^[yj]}i){
        $key = 1
      } elsif ($key =~ m{^n}i) {
        $key = 0
      } elsif ($key =~ m{^w}i) {
        $key = $always_write = 1
      } elsif ($key =~ m{^s}i) {
        &normal_exit()
      } else {
        $key = ''
      }
    }
    next unless ($key)
  }
  my $written_ok = 1;
  my ($atime, $mtime);
  ($atime, $mtime) = ((stat($file_name))[8, 9]) if ($keep_timestamp);
  open($fh, '>', $file_name) or
    &fatal("could not open $file_name for writing");
  binmode($fh) if ($binary);
  for my $i (@content) {
    $written_ok = '' unless (print($fh $i))
  }
  $written_ok = '' unless (close($fh));
  &fatal("error writing to $file_name") unless ($written_ok);
  utime($atime, $mtime, $file_name) if ($keep_timestamp)
}
&normal_exit()
