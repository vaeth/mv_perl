#!/usr/bin/env perl
BEGIN { require 5.006_001 }
package Rendirs;
our $VERSION = 'v5.10.5';
eval { $VERSION = version->declare($VERSION) };
eval 'use version 0.77; $VERSION = version->declare($VERSION)' if ($@);

use warnings;
use strict;
use integer;
use Getopt::Long 2.24 ();
#use Pod::Usage ();
#use String::ShellQuote ();  # not mandatory but recommended: poor fallback

=encoding UTF-8

=head1 NAME

rendirs - Rename files (and directories) according to a description file

=head1 SYNOPSIS

B<rendirs> [options] I<Filelist>

To get an extended help, type B<rendirs --man> or B<rendirs -?>

=head1 DESCRIPTION

One sometimes has to rename a list of files from "long" (original) names
into "short" (e.g. DOS compliant) names or vice versa.

If one wants to do this for a fixed directory tree more than once (e.g. in
order to "save" a directory in DOS compliant format to a CD-Rom and
to "restore" it later), it is convenient to write just a list of corresponding
filenames and to have a script which interprets this list and renames the
files correspondingly. This here is such a script, and the list of filenames
is the argument I<Filelist>.

(As usual in perl, the argument I<Filelist> can consist of several names;
the corresponding data is then concatenated. Moreover, the name F<-> is
magic and means that the list is read from standard input).

The list is expected to be a list of lines with the following meaning:

=over 8

=item Two words (separated by spaces/tabs)

The first word is considered as the original filename, the
second as the short name.

=item One word

The word is considered as a filename whose original name is the
same as the short name (by default, then only its existence checked)

=item A single word, ending with a colon (C<:>)

The word is considered as a directory and means that all following files
occur in this directory. Note that this refers to files but B<not> to
directories (i.e. not to the next entry of this type). However, there
are some ways to change this behavimy: There are magic symbols C<*> and C<?>
available at the beginning or end of directory names with the following
meaning.

=over 4

=item

C<*> at the end of the name means that the directory is a ``parent directory''
for all following directories (of cmyse, directories starting with B</> will
not be affected). Thus, roughly speaking, this has a similar meaning as a
C<cd> into the directy ``after the directory was handled''.

=item

C<?> at the end of the name cancels the effect of the previous
``parent directory'' entry for all following directories.
The action is cummulative (and more than one C<?> can be used
at the end of one name).
Thus, roughly speaking, this has a similar meaning as a C<cd ..>
(where C<..> denotes the previous active directory)
``after the directory was handled''.

=item

C<*?> or C<?*> at the end of the name cancels the effect of all
previous ``parent directory'' entries.
Thus, roughly speaking, this has a similar meaning as a C<cd />
(where C</> denotes the starting directory of B<rendirs>)
``after the directory was handled''.

=item

C<*> at the beginning of the name means that the current directory name
is to be understood as a subdirectory of the previous directory entry
(if there was no previous directory entry, this has no effect).
Thus, roughly speaking, the C<*> at the beginning has the meaning as
preceeding C<previous_directory/> to the directory name.

=item

C<?> at the beginning of the name means that for the current directory
the effect of the previous active C<*> (at the directory end) is canceled.
More than one C<?> at the beginning are allowed (with cummulative effect).
Thus, roughly speaking, the C<?> at the beginning has a similar meaning as
preceeding C<../> to the directory name.

=item

C<*?> or C<*?> at the beginning of the name means that for the current
directory the effect of all C<*> (at the directory end) is canceled.
Thus, roughly speaking, this has a similar meaning as
preceeding C</> to the directory name.

=item

The empty name means that no special directory name is used:
The current directory is then the previous (non-cancelled) ``parent directory''
(resp. the starting directory if none exists).

=back

In view of the ``empty name'' convention, it is admissible that a name
consists only of these magic symbols. In this case, these symbols are
considered to be ``at the end of the empty name''.
This means that such a name does not affect the following ``standard''
entries but only the following ``directory'' entries. If you want to
influence the standard ``entries'', you can follow it by the line C<:>
(containing only the empty name).

For better readability, the special symbols may be separated by a C</> at
the beginning or end so that e.g. C<*/subdir> has the same meaning as
C<*subdir>, and C<main/*> has the same meaning as C<main*>.

=item Two words, at least one ending with a colon (C<:>)

Like a single word with a colon, but additionally the directory is renamed
(as if listed with two words). The above magic symbols have an analogous
meaning as above, but only for the name in which they occur.
Note that this means that you usually want to use these special symbols either
in both names or in none of the names, unless you want a very special behavimy
and have considered thoroughly what you are doing.

=item C<#@ShortNamesFirst> (case is ignored, but spaces may not be contained)

After this magic line, the interpretation of the names in the file is reversed,
i.e. first the short name and then the original name is expected. You can
switch back anytime within the file using the magic line:

=item C<#@OriginalNamesFirst> or C<#@LongNamesFirst> (case is ignored)

This switches back to the original interpretation of the names.

=item C<#@RequireMode> (case is ignored)

This line causes to break with an error unless one of the options
B<--short> (or B<-s>) or B<--long> (or B<-l>) was sepecified.

=item C<#@MagicDots> (case is ignored)

After this magic line, the interpretation of the names in the file is
as if the option B<--magicdots> (or B<-m>) had been given, i.e.
if only one (file or directory) name appears in a line, this name is
interpreted as the "long" name while the "short" name is then assumed
to be the magic name ".".
You can switch back anytime within the file using the magic line:

=item C<#@NoMagicDots> (case is ignored)

This switches back to the original interpretation of the names.

=item C<#@UpperCase> (case is ignored)

=item C<#@LowerCase> (case is ignored)

After one of these lines, I<Filelist> is interpreted as if the option
B<--uppercase> (resp. <--lowercase>) was given. If none of the options
B<--short> (or B<-s>) or B<--long> (or B<-l>) was sepecified,
this line has no effect, but a corresponding warning is printed.

=item C<#@NoUpperCase> (case is ignored)

=item C<#@NoLowerCase> (case is ignored)

This cancels the effect of a previous C<#@UpperCase>/C<#@LowerCase> line or
of the B<--uppercase>/B<--lowercase> option. These lines never cause a warning.

=item C<#@Quiet> (case is ignored)

=item C<#@NoQuiet> (case is ignored)

These lines trigger the effect of the B<--quiet> (or B<-q>) option
resp. negate this effect.

=item C<#@{> (no spaces may be contained between these symbols)

Ignore all lines until a line starts with

=item C<#@}> (no spaces may be contained between these symbols)

Nesting of comments is supported.

=item Anything following a C<#> is considered as a comment and thus ignored.

=back

Except for the word separation all spaces/tabs are ignored.
Empty lines are ignored.

All files listed are renamed: Either from short name into the original name
or vice versa (By default, depending which one already exists).
Note that this may cause unexpected behavimy if a filename appears double
in the list (even if it appears once as a "long" name and once as a "short"
name): The list is always processed in its order!
Therefore, to avoid unintended side effects, it is strongly recommended to
use always one of the options B<-s> or B<-l>; the default is only for
extreme lazyness or if you know exactly what you are doing.

The filename F<.> is magic and has the following meaning:
If the file should be renamed from F<.>, then an empty file will be created.
If an empty file should be renamed into F<.>, then it will be deleted
(non-empty files are not deleted unless option B<--delete> is used).

If the filename contains the magic sequence F<-E<gt>> it is considered as
a symbolic link where the part before the sequence is the name of the link
and the part after the sequence is the place where the link points to.
It is explicitly admissible to change both parts for the long and short names
and to use this option in connection with the magic filename F<.> (the latter
means of cmyse that not an empty file but the corresponding link will be
created respectively deleted).

Since Revision 5.1 of this program, the magic name F<.> is also allowed for
directories and has a double meaning: On the one hand, it means indeed ".",
but on the other hand, it also has a similar magic meaning as for filenames.
In particular, this name can be used to create or delete directories.
Although all actions are always done according to the order in the argument
file(s), deletion of directories (or of links pointing to directories) is the
only exception of this rule: This action is postponed until all other actions
are done, and then these actions are executed in reverse order (i.e. it is
expected that the argument file(s) contain first the parent directories and
then their subdirectories, similar to the output of "ls -R").

Directories are only deleted if they are empty and, for security reasons,
there is no option to change this behavior.

=head1 OPTIONS AND ARGUMENTS

=over 8

=item B<--help> or B<-h>

Display brief help.

=item B<--man> or B<-?>

Display extended help as a manpage.

=item B<--long> or B<-l>

Always rename to long (original) filenames.

=item B<--short> or B<-s>

Always rename to short filenames.

=item B<--paranoia> or B<-p>

Stop processing if a file in the list does not exist.

=item B<--continue> or B<-c>

Continue processing, even if a file cannot be renamed because the
destination file exists (in no case the destination file is overridden)

=item B<--delete>

Delete also nonempty files if they are renamed into F<.>.

=item B<--dotonly> or B<-d>

Do not rename any files: Only create or delete the empty files,
i.e. care only about the lines the magic name F<.> (and the lines
describing the directory names, of cmyse).

=item B<--ignoredot> or B<-D>

This is quite the opposite of --dotonly: This ignores all entries
concerning the magic filename F<.>.

=item B<--shortnamesfirst> or B<-S>

Behave as if the first line of the file is @ShortNamesFirst

=item B<--magicdots> or B<-m>

If there is only one filename/directoryname in a line, interpret it as the
"long" filename and assume that the "short" filename is ".". This is useful
if you want to use e.g. the output of "ls -R" to recreate a directory
structure (consisting only of empty files) for testing or synchronization
purposes.

=item B<--uppercase>

If a file or directory cannot be found, look also for the uppercase version
of the filename. Note that this may cause unexpected results if two names
in I<Filelist> have the same uppercase version. To avoid completely
unexpected side effects, this option is admissible only in conjunction
with the options B<--long> (or B<-l>) or B<--short> (or B<-s>).

=item B<--lowercase>

This is the same as B<--uppercase> just that the lowercase version of the
filename is used. If used together with <--uppercase>, then first the
uppercase version is tested and only then the lowercase version is used.

=item B<--showonly> or B<-N>

Show only what would be done, do not rename anything. Note that this can give
a different output than the actual run, because testing is always based on the
current state of the filesystem. In particular, in connection with renaming
directories or if filenames occur double in long and short versions
common to long and short versions, you will not see
the expected output.

=item B<--dry-run> or B<-n>

This is similar than B<--showonly>, but in connection with renaming
directories it behaves differently: The files are expected to be in
the unremaned directory (even if the directory was renamed).
Hence, in contrast to B<--showonly> there does not arise wrong behavimy
due to the fact that files cannot be found because they have not moved
with their parent directory (because the parent directory was actually not
renamed). But be aware that a "proper" renaming (without this option)
would have to use different paths!

=item B<--quiet> or B<-q>

Do not show the actions.

=item B<--version> or B<-V>

Print version number

=back

=head1 AUTHOR

Martin VE<auml>th E<lt>martin@mvath.deE<gt>

=cut

# Default for the Options:

my $original = '';
my $short = '';
my $continue = '';
my $paranoia = '';
my $delete = '';
my $dotonly = '';
my $ignoredot = '';
my $shortfirst = '';
my $showonly = '';
my $dryrun = '';
my $quiet = '';
my $uppercase = '';
my $lowercase = '';
my $magicdots = '';

# Parse Options:

Getopt::Long::Configure(qw(gnu_getopt));
Getopt::Long::GetOptions(
	'help|h', sub { &pod2usage(0) },
	'man|?', sub { &pod2usage(-verbose => 2, -exit => 0) },
	'long!', \$original,
	'l', \$original,
	'nol', sub { $original = '' },
	'short!', \$short,
	's', \$short,
	'nos', sub { $short = '' },
	'uppercase!', \$uppercase,
	'lowercase!', \$lowercase,
	'continue!', \$continue,
	'c', \$continue,
	'noc', sub { $continue = '' },
	'paranoia!', \$paranoia,
	'p', \$paranoia,
	'nop', sub { $paranoia = '' },
	'delete!', \$delete,
	'dotonly!', \$dotonly,
	'd', \$dotonly,
	'nod', sub { $dotonly = '' },
	'ignoredot!', \$ignoredot,
	'D', \$ignoredot,
	'noD', sub { $ignoredot = '' },
	'shortnamesfirst!', \$shortfirst,
	'S', \$shortfirst,
	'noS', sub { $shortfirst = '' },
	'magicdots!', \$magicdots,
	'm', \$magicdots,
	'nom', sub { $magicdots = '' },
	'showonly!', \$showonly,
	'N', \$showonly,
	'noN', sub { $showonly = '' },
	'dry-run!', \$dryrun,
	'n', \$dryrun,
	'non', sub { $dryrun = '' },
	'quiet!', \$quiet,
	'q', \$quiet,
	'version|V', \&version
) or &pod2usage(2);
&pod2usage(2) unless (@ARGV);

$showonly = 1 if ($dryrun);

my $mode = 0;
if ($original) {
	$mode = -1
} elsif ($short) {
	$mode = 1
} elsif ($lowercase || $uppercase) {
	&fatal('--uppercase or --lowercase invalid without --short or --long')
}

my @REVERSEACTION = ();
my $currdir = '';
my $followdir = [''];

sub analyzedir {
	my($n) = @_;
	my $parnum = $#{$followdir};    # Default parent Nr.
	my $p = ${$followdir}[$parnum]; # Default parent
	my $follow = [@$followdir];     # Copy the $followdir array.
	my $willpush = '';
	if ($n =~ s{\s*([\*\?][\s\*\?]*)\z}{}m) { # special symbols at the end:
		my $s = $1;
		while () {
			if (($s =~ s{\A\?\*\s*}{}m) || ($s =~ s{\A\*\?\s*}{}m)) {
				$follow = ['']; $willpush = ''
			} elsif ($s =~ s{\A\?\s*}{}m) {
				$willpush = '';
				pop(@$follow) if ($#{$follow} > 0)
			} elsif ($s =~ s{\A\*\s*}{}m) {
				$willpush = 1
			} else {
				last
			}
		}
	}
	$n =~ s{([^\s][^\/]*)\/+\s*$}{$1}m; # ignore / at directory end

	if ($n =~ m{\A\s*\/}m) {
		$p = ''
	} elsif ($n =~ s{\A\s*([\*\?][\s\*\?]*)}{}m) {
		# special symbols at the beginning:
		my $s = $1;
		while () {
			if (($s =~ s{\A\?\*\s*}{}m) || ($s =~ s{\A\*\?\s*}{}m)) {
				$parnum = 0; $p = ${$followdir}[0]
			} elsif ($s =~ s{\A\?\s*}{}) {
				if ($parnum > 0) {
					--$parnum
				}
				$p = ${$followdir}[$parnum]
			} elsif ($s =~ s{\A\*\s*}{}) {
				$p = $currdir
			} else {
				last
			}
		}
		$n =~ s{^\/+}{}  # special beginning but directory starts with /: Ignore /
	}
	($n, $p, [$follow, $willpush])
}

sub setdirs {
	my ($p, $f) = @_;
	($followdir, $f) = @$f;
	if ($p eq '') {
		$currdir = ''
	} else {
		$currdir = $p . '/'
	}
	push(@$followdir, $currdir) if ($f)
}

sub analyzelink {
	my ($n) = @_;
	if ($n =~ m{\A(.*)\-\>(.*)\z}m) {
		my($n, $l) = ($1, $2);
		return ($n, $l) unless ($n eq '.');
		&warn("magic name '.' must not be a link; ignoring ->$l")
	}
	($n, undef)
}

sub casetest {
	my ($p, $n, $s) = @_;
	my $c = $p . $n;
	return ($c, 1, $s) if ((-e $c) || (-d $c) || (-l $c));
	if ($uppercase) {
		my $d = $p . uc($n);
		return ($d, 1, '') if ((-e $d) || (-d $d) || (-l $d))
	}
	if ($lowercase) {
		my $d = $p . lc($n);
		return ($d, 1, '') if ((-e $d) || (-d $d) || (-l $d))
	}
	($c, 0, $s)
}

sub myunlink {
	my ($n) = @_;
	&shprint('rm', $n) unless ($quiet);
	unlink($n) unless ($showonly)
}

sub myrmdir {
	my ($n) = @_;
	my @cmd = &shdash(['rmdir', '-p'], $n);
	&shprintraw(@cmd) unless ($quiet);
	system(@cmd) unless ($showonly)
}

sub mytouch {
	my ($n) = @_;
	&shprintraw('touch', $n) unless ($quiet);
	unless ($showonly) {
		open(my $fh, '>', $n);
		close($fh)
	}
}

sub mymkdir {
	my($n) = @_;
	my @cmd = &shdash(['mkdir', '-p'], $n);
	&shprintraw(@cmd) unless ($quiet);
	system(@cmd) unless ($showonly)
}

sub mylink {
	my ($a, $b) = @_;
	my @cmd = &shdash(['ln', '-s'], $a, $b);
	&shprintraw(@cmd) unless ($quiet);
	system(@cmd) unless ($showonly)
}

sub myrename {
	my ($a, $b) = @_;
	&shprint('mv', $a, $b) unless ($quiet);
	rename($a, $b) unless ($showonly)
}

sub action {
	my ($o, $s) = @_;
	# Besides some flags there are fmy types of variable names:
	# First, concerning the short ("s") or original ("o") file/dirnames.
	# Later, concerning the smyce ("smyce") or destination ("dest") names.
	# (They will correspond to each other, depending on the mode and existence).
	# To this base name, a letter is appended, concerning what is meant:
	# 'e': Does the file exist or is it a magic dot?
	# 'p': (Parent) path
	# 'f': For dirs: Reference for new $followdir to be handled in &setdir.
	# 'l': For links: The name of the link.

	# Parse names: Do we deal with directories and/or two arguments?
	my ($sp, $op, $sf, $of);
	my $isdir = '';
	$isdir = 1 if ($s =~ s{\s*:\s*\z}{}m);
	$isdir = 1 if ($o =~ s{\s*:\s*\z}{}m);
	if ($isdir) {
		($s, $sp, $sf) = &analyzedir($s);
		($o, $op, $of) = &analyzedir($o);
	} else {
		$sp = (($s =~ m{^\/}) ? '' : $currdir);
		$op = (($o =~ m{^\/}) ? '' : $currdir);
		$sf = $of = ''
	}
	my($sl, $ol);
	($s, $sl) = &analyzelink($s);
	($o, $ol) = &analyzelink($o);
	my $link = '';
	my $samelink = undef;
	if (defined($sl) || defined($ol)) {
		$link = 1;
		$samelink = ((!defined($sl)) || (!defined($ol)) || ($sl eq $ol))
	}
	my $same = '';
	$same = 1 if (($s eq $o) && ((!$link) || $samelink));

	# Decide which is smyce and which is destination
	my ($smyce, $dest, $smycee, $deste, $smycef, $destf, $destl);
	{
		my $oe = 0;
		my $se = 0;
		$oe = -1 if ($o eq '.');
		$se = -1 if ($s eq '.');
		return if ($ignoredot && (($oe < 0)||($se < 0)));
		my $currmode = $mode;
		if ($mode > 0) {
			$s = "$sp$s";
			$se = 1 if ((!$se) && ((-e $s) || (-d $s)) || (-l $s));
			($o, $oe, $same) = &casetest($op, $o, $same) if (!$oe);
		} elsif ($mode < 0) {
			$o = "$op$o";
			$oe = 1 if ((!$oe) && ((-e $o) || (-d $o) || (-l $o)));
			($s, $se, $same) = &casetest($sp, $s, $same) if (!$se);
		} else {
			$s = "$sp$s"; $o = "$op$o";
			$oe = 1 if ((!$oe) && ((-e $o) || (-d $o) || (-l $o)));
			if ($same) {
				$se = $oe;
				return if ($oe < 0)
			}
			$se = 1 if ((!$se) && ((-e $s) || (-d $s)) || (-l $s));
			if ($se < 0) {
				$currmode = ($oe ? 1 : -1)
			} else {
				$currmode = ($se ? -1 : 1)
			}
		}
		if ($currmode > 0) {
			$smyce = $o; $smycee = $oe; $smycef = $of;
			$dest = $s;   $deste = $se;   $destf = $sf;  $destl = $sl
		} else {
			$smyce = $s; $smycee = $se; $smycef = $sf;
			$dest = $o;   $deste = $oe;   $destf = $of;  $destl = $ol
		}
	}

	# Set new directory paths:

	if ($isdir) {
		if (($deste < 0) || $dryrun) {
			&setdirs($smyce, $smycef)
		} else {
			&setdirs($dest, $destf)
		}
	}

	# If required, we are now ready to return

	return if ($isdir && $same && ($s eq ''));
	if ($dotonly) {
		return if (($smycee >= 0) && ($deste >= 0))
	}

	# Now the action:
	if ($smycee) { # Smyce exists or is magic
		unless ($same) {
			if ($deste > 0) { # Destination exists
				if (($link) && (-l $dest)) {
					&myunlink($dest);
					if ($samelink) {
						&myrename($smyce, $dest)
					} else {
						&mylink($destl, $dest)
					}
				} else {
					&warn_or_fatal("file/directory $dest already exists");
				}
			} elsif ($deste < 0) {  # Destination is magic (then Smyce is nonmagic)
				if ($isdir) {
					if ($link && (-l $smyce)) {
						push(@REVERSEACTION, [\&myunlink, $smyce])
					} else {
						push(@REVERSEACTION, [\&myrmdir, $smyce])
					}
				} elsif ((-z $smyce) || ($link && (-l $smyce))) {
					&myunlink($smyce)
				} elsif ($delete) {
					&warn("deleting nonempty file $smyce");
					&myunlink($smyce)
				} else {
					&warn_or_fatal("nonempty file $smyce is not deleted")
				}
			} else {  # Destination does not exist
				if ($smycee > 0) {  # Smyce is nonmagic
					if ((!$link) || $samelink) {
						&myrename($smyce, $dest)
					} else {
						if (-l $smyce) {
							&myunlink($smyce);
							&mylink($destl, $dest);
						} else {
							&warn_or_fatal(($dest eq $smyce) ?
								"file $smyce is not turned into a link to $destl" :
								"file $smyce cannot be renamed into link $dest->$destl")
						}
					}
				} else {  # Smyce is magic
					unless ($link) {
						if ($isdir) {
							&mymkdir($dest)
						} else {
							&mytouch($dest)
						}
					} else {
						&mylink($destl, $dest)
					}
				}
			}
		}
	} else {  # Smyce does not exist (and is nonmagic):
		&fatal_or_warn($paranoia, $mode ?
			"File/Directory $smyce does not exist" :
			"Neither $dest nor $smyce exists")
	}
}

sub errorunlessmode {
	return if ($mode);
	&fatal_or_warn($_[0], " line $.: ",
		"none of the options --short -s --long -l is specified")
}

my $commentdepth = 0;
sub handleline {
	chomp();
	my $currline = $_;
	if (m{^\s*\#\@\{}i) {
		++$commentdepth;
		return
	}
	if (m{^\s*\#\@\}}i) {
		&fatal("line $.: ", 'closing comment without opening one')
			unless ($commentdepth);
		--$commentdepth;
		return
	}
	return if ($commentdepth);
	if (m{^\s*\#\@shortnamesfirst}i) {
		$shortfirst = 1;
		return
	}
	if (m{^\s*\#\@(?:(?:original)|(?:long))namesfirst}i) {
		$shortfirst = '';
		return
	}
	if (m{^\s*\#\@requiremode}i) {
		&errorunlessmode(1);
		return
	}
	if (m{^\s*\#\@magicdots}i) {
		$magicdots = 1;
		return
	}
	if (m{^\s*\#\@nomagicdots}i) {
		$magicdots = '';
		return
	}
	if (m{^\s*\#\@uppercase}i) {
		$uppercase = 1;
		&errorunlessmode('');
		return
	}
	if (m{^\s*\#\@lowercase}i) {
		$lowercase = 1;
		&errorunlessmode('');
		return
	}
	if (m{^\s*\#\@nouppercase}i) {
		$uppercase = '';
		return
	}
	if (m{^\s*\#\@nolowercase}i) {
		$lowercase = '';
		return
	}
	if (m{^\s*\#\@quiet}i) {
		$quiet = 1;
		return
	}
	if (m{^\s*\#\@noquiet}i) {
		$quiet = '';
		return
	}
	s{\#.*$}{};
	my @A = split();
	if (@A == 1) {
		if ($magicdots) {
			&action($A[0], '.')
		} else {
			&action($A[0], $A[0])
		}
	} elsif (@A == 2) {
		if ($shortfirst) {
			&action($A[1], $A[0])
		} else {
			&action($A[0], $A[1])
		}
	} elsif (@A) {
		&fatal("line $.: ", 'more than two words are not allowed',
			"\n", $currline)
	}
}

for my $file (@ARGV) {
	my ($fh, $close);
	if ($file eq '-') {
		$fh = \*STDIN;
		$close = ''
	} else {
		unless (open($fh, '<', $file)) {
			&warn('cannot open ', $file);
			next
		}
		$close = 1
	}
	while (<$fh>) {
		&handleline()
	}
	close($fh) if ($close)
}

while (@REVERSEACTION) {
	my ($func, $arg) = @{pop(@REVERSEACTION)};
	$func->($arg)
}
&warn('file ends with unclosed comment') if ($commentdepth);

sub shdash {
	my $c = shift();
	$c = [$c] unless (ref($c) eq 'ARRAY');
	for my $i (@_) {
		if ($i =~ m{^[-+]}o) {
			push(@$c, '--');
			last
		}
	}
	(@$c, @_)
}

# use String::ShellQuote () and return whether successful

{ my $shellquote = undef;  # A closure static variable
sub use_shellquote {
	return $shellquote if (defined($shellquote));
	eval {
		require String::ShellQuote
	};
	$shellquote = !$@
}}

# like join(' ', @_), but shell-quote arguments

sub join_quoted {
	my @r;
	for my $i (@_) {
		my $a = $i;
		$a =~ s{\'}{\'\\\'\'}g;
		$a = "'$a'";
		$a =~ s{(\A|[^\\])\'([\w\-\,\.\:\/]*)\'}{$1$2}gm;
		push(@r, ($a ne '') ? $a : "''")
	}
	join(' ', @r)
}

sub shell_quote_best_effort {
	(&use_shellquote() ?
		&String::ShellQuote::shell_quote_best_effort : &join_quoted)
}

sub shprintraw {
	print(&shell_quote_best_effort, "\n")
}

sub shprint {
	&shprintraw(&shdash)
}

sub error {
	print(STDERR 'rendirs: ', @_, "\n")
}

sub fatal {
	&error;
	exit(1)
}

sub warn {
	print(STDERR 'warning: ', @_, "\n")
}

sub version {
	print('rendirs ', $VERSION, "\n");
	exit(0)
}

sub pod2usage {
	require Pod::Usage;
	&Pod::Usage::pod2usage
}

sub warn_or_fatal {
	$continue ? &warn : &fatal
}

sub fatal_or_warn {
	my $a = shift();
	$a ? &fatal : &warn
}
