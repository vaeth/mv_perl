#compdef touchdirs
# SPDX-License-Identifier: BSD-3-Clause
_arguments -s -S : \
'(* -)'{'--help','-h'}'[print a brief help]' \
'(* -)'{'--man','-\\?'}'[show extended help as a manpage]' \
'(* -)'{'--version','-V'}'[print version]' \
{'--ignore=','-i+'}'[ignore regexp path]:regexp:(/ "/[^/]*\$")' \
{'--ignore-case=','-I+'}'[ignore regexp path, ignoring case]:regexp:(/ "/[^/]*\$")' \
'*'{'--quiet','-q'}'[quiet for ignored files/and commands]' \
{'(-v)--verbose','(--verbose)-v'}'[inform about empty dirs]' \
{'(-A)--access','(--access)-A'}'[let access time equal mtime]' \
{'(-m)--mtime','(--mtime)-m'}'[do not write atime only for rounding]' \
'(-a --all '{'-f)--force','--force)-f'}'[force touching dirs]' \
'(-r --round -R --roundread)--rounding[round dir timestamps to seconds]' \
'(--rounding -r --round '{'-R)--roundread','--roundread)-R'}'[round timestamps when reading]' \
'(--rounding -R --roundread '{'-r)--round','--round)-r'}'[round all timestamps to seconds]' \
'(-f --force '{'-a)--all','--all)-a'}'[force touching all files]' \
{'(-d --dot -g --git)--dotdir','(--dotdir --dot -g --git)-d'}'[ignore .* dirs]' \
{'(-D --dot)--dotfile','(--dotfile --dot)-D'}'[ignore .* files]' \
'(-d --dotdir -D --dotfile -g --git)--dot[ignore .* dirs]' \
{'(-g)--git','(--git)-g'}'[ignore .git dirs]' \
{'(-e --empty-ignore)--empty','(--empty-ignore -e)-empty','(--empty-ignore --empty)-e'}'[ignore empty dirs]' \
{'(-c)--check','(--check)-c'}'[check whether Time::HiRes works]' \
{'(--dry-run -n '{'--show)-s','-s)--show'},'(--show -s '{'--dry-run)-n','-n)--dry-run'}}'[only show what would be done]' \
{'(--follow-link)-F','(-F)--follow-link'}'[follow links for recursion and getting times]' \
'*:dirs:_files -/'
