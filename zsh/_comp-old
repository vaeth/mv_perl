#compdef comp-old
# SPDX-License-Identifier: BSD-3-Clause
_arguments -s -S : \
'(1 * -)'{'--help','-h'}'[print a brief help]' \
'(1 * -)'{'--man','-\\?'}'[show extended help as a manpage]' \
'(1 * -)'{'--version','-V'}'[print version]' \
{'(--down)-d','(-d)--down'}'[do not compare metadata of highest directory]' \
{'(--norecurse)-r','(-r)--norecurse'}'[do not compare recursively]' \
{'(--nocomp)-c','(-c)--nocomp'}'[do not compare content of the files]' \
{'(--ignore-case)-i','(-i)--ignore-case'}'[ignore case in paths]' \
{'(--verbose)-v','(-v)--verbose'}'[verbose file names]' \
{'(--debug)-D','(-D)--debug'}'[debug]' \
{'(--noresolve)-l','(-l)--noresolve'}'[do not resolve symlinks]' \
{'(--pedantic)-p','(-p)--pedantic'}'[compare other data like hardlink number]' \
{'(--too-pedantic)-P','(-P)--too-pedantic'}'[compare other data like device identifiers]' \
{'(--only-first)-1','(-1)--only-first'}'[do not complain if LastDir contains unmatched files]' \
{'(--only-last)-2','(-2)--only-last'}'[do not complain if LastDir does contain required file]' \
{'(--tex)-x','(-x)--tex'}'[TeX Mode\: Ignore *.dvi *.aux *.toc ...]' \
{'(--neverwarn --nowarn --noowner --notime --nodirtime --noattr -W -o -t -T -a)-w','(--nowarn --noowner --notime --nodirtime --noattr -W -o -t -T -a -w)--neverwarn'}'[do not print warnings]' \
{'(--nowarn)-W','(-W)--nowarn'}'[do not print warnings about strange attributes]' \
{'(--noowner)-o','(-o)--noowner'}'[do not warn about differing ownership]' \
{'(--notime --nodirtime -T)-t','(--nodirtime -T -t)--notime'}'[do not warn about differing dir timestamps]' \
{'(--noattr)-a','(-a)--noattr'}'[do not warn about differing attributes]' \
'*:mask:_files'
